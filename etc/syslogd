#!/usr/bin/perl
use strict;
use warnings;
use IO::Socket::INET;
use Getopt::Long;

# Define UDP port for syslog
my $port = 514;
my $logfile = '/tmp/log.log';

# Get command-line options
GetOptions("port=i" => \$port, "file=s" => \$logfile) or die "Usage: $0 [--port <port_number>] [--file <CSV file>]\n";

# Create UDP socket
my $socket = IO::Socket::INET->new(
	LocalPort => $port,
	Proto => 'udp'
) or die "Could not create socket: $!\n";

print "Syslog server listening on UDP port $port...\n";

if(!-r $logfile) {
	# First run - put in the heading row
	open(my $log, '>', $logfile);
	print $log '"IP","facility","severity","msg"',
		"\n";
	close $log;
}

open(my $fout, '>>', $logfile);

while (1) {
	my $message;
	my $remote_address = $socket->recv($message, 1024);

	if ($remote_address) {
		my ($port, $ip) = unpack_sockaddr_in($remote_address);
		my $remote_ip = inet_ntoa($ip);
		chomp $message;
		
		# Extract PRI field
		if($message =~ /^<(\d+)>(.*)/) {
			my $pri = $1;
			my $log_message = $2;

			my $facility = int($pri / 8);
			my $severity = $pri % 8;

			print $fout "\"$remote_ip\",\"$facility\",\"$severity\",\"$log_message\"\n";
		} else {
			print $fout "\"[$remote_ip\",\"\",\"\",\"Invalid syslog message: $message\"\n";
		}
	}
	$fout->flush();
}

# Close socket (unreachable in an infinite loop)
$socket->close();
